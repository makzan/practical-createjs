== Animated tiles chart

1. Similar to the last example, we prepare the `chart.coffee` file with the basic structure: `constructor`, `initChart` and `drawChart`.
+
.chart.coffee
[source,coffeescript]
----
class this.app.Chart
  # Entry point.
  constructor: (canvasId)->
    @stage = new cjs.Stage(canvasId)

    cjs.Ticker.setFPS(60)
    cjs.Ticker.addEventListener 'tick', @stage

    utility.retinalize @stage, false
    @canvasWidth = utility.originalCanvasWidth
    @canvasHeight = utility.originalCanvasHeight

    @initChart()
    @lastNumberOfTiles = 0


  initChart: ->
    @stage.removeAllChildren()
    # Init code later


  drawChart: (value) ->
    # Code later
----

2. The `initChart` method will draw all the tiles in the canvas area. By default all the tiles has 0 scaling so they are not visible at the beginning.
+
.chart.coffee
[source,coffeescript]
----
initChart: ->
  @stage.removeAllChildren()
  tileWidth = tileHeight = 10
  margin = 5
  leadingMargin = margin + tileWidth / 2

  chartArea = (@canvasWidth-margin-leadingMargin) * (@canvasHeight-margin-leadingMargin)
  tileArea = (tileWidth + margin) * (tileHeight + margin)
  @maxNumberOfTiles = Math.floor( chartArea / tileArea )
  cols = Math.floor((@canvasWidth-margin) / (tileWidth + margin))

  @shapes = []
  for i in [0...@maxNumberOfTiles]
    x = leadingMargin + Math.floor(i % cols) * (tileWidth + margin)
    y = leadingMargin + Math.floor(i / cols) * (tileHeight + margin)
    shape = new RectShape
      fillColor: 'ORANGERED'
      width: tileWidth
      height: tileHeight
      x: x
      y: y
    @stage.addChild shape
    @shapes.push shape

    shape.regX = tileWidth/2
    shape.regY = tileHeight/2
    shape.scaleX = shape.scaleY = 0
----

3. The `drawChart` method doesnâ€™t draw the chart. It actually toggle the `scaleX` and `scaleY` of the existing tiles. It will scale up or down the tiles based on the given value.
+
.chart.coffee
[source,coffeescript]
----
drawChart: (value) ->
  # each tile = 100K km2
  areaForEachTile = 100
  numberOfTiles = Math.floor(value / areaForEachTile)
  for i in [0...@maxNumberOfTiles]
    if i < numberOfTiles
      delay = (i - @lastNumberOfTiles) * 5
      Tween.get(@shapes[i]).wait(delay).to({scaleX:1, scaleY:1}, 400, Ease.quartOut)
    else
      delay = (i - @lastNumberOfTiles) * 2
      Tween.get(@shapes[i]).wait(delay).to({scaleX:0, scaleY:0}, 400, Ease.quartOut)
  @lastNumberOfTiles = numberOfTiles
----

4. In the `view.coffee`, we ensure the logic to toggle both chart is correct as the following. It is the same as the project 4 code.
+
.view.coffee
[source,coffeescript]
----
this.app.handleListChange = (chart1, chart2)->
  # Toggle Chart 1
  $('input[type="radio"]').change ->
    value = $('input[type="radio"]:checked').val()
    $('.output1').text(Math.round(value))
    chart1.drawChart(value)

  # Toggle Chart 2
  $('input[type="checkbox"]').change ->
    sum = 0
    $('input[type="checkbox"]:checked').each ->
      sum += $(this).val()*1
    $('.output2').text(Math.round(sum))
    chart2.drawChart(sum)
----

=== What just happened?

We have created a nice animated tile-based bar chart.
